<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.12">
  <POU Name="FB_Axis" Id="{45901cd0-c6d2-4114-b7cf-de832171219f}" SpecialFunc="None">
    <Declaration><![CDATA[///#########################################################
///Function block to run a virtual drive with Nc
///	Library:		
///	Tc2_MC2.lib
///
///	Global Variables:
///	
///	Data types:
///	
///	External functions:
///
///###########################################################
FUNCTION_BLOCK FB_Axis
VAR
	sVersion: STRING:='1.0.4';	
	
END_VAR
VAR_INPUT

END_VAR
VAR_OUTPUT

END_VAR

VAR_IN_OUT
	stAxisStruct: ST_AxisStruct;
END_VAR
VAR	
	fbReset: MC_Reset;
	fbPower: MC_Power;
	fbStop: MC_Stop;
	fbHalt: MC_Halt;
	fbJog: MC_Jog;
	fbMoveAbsolute: MC_MoveAbsolute;
	fbMoveVelocity: MC_MoveVelocity;
	fbMoveRelative: MC_MoveRelative;
	fbMoveModulo: MC_MoveModulo;	
	fbGearIn: MC_GearIn;
	fbGearOut: MC_GearOut;
	fbHome: FB_Homing;
	fbExecuteRiseEdge: R_TRIG;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[stAxisStruct.Axis.ReadStatus();


////Call of all MC Instances//////

fbPower(Axis:=stAxisStruct.Axis);
fbPower.Enable:= FALSE;
fbReset(Axis:=stAxisStruct.Axis);
fbReset.Execute:= FALSE;
fbStop(Axis:=stAxisStruct.Axis);
fbStop.Execute:=FALSE;
fbHalt(Axis:=stAxisStruct.Axis);
fbHalt.Execute:=FALSE;
fbJog(Axis:=stAxisStruct.Axis);
fbHome(Axis:=stAxisStruct.Axis);
fbHome.bExecute:=FALSE;
fbMoveAbsolute(Axis:=stAxisStruct.Axis);
fbMoveAbsolute.Execute:=FALSE;
fbMoveRelative(Axis:=stAxisStruct.Axis);
fbMoveRelative.Execute:=FALSE;
fbMoveVelocity(Axis:=stAxisStruct.Axis);
fbMoveVelocity.Execute:=FALSE;
fbMoveModulo(Axis:=stAxisStruct.Axis);
fbMoveModulo.Execute:=FALSE;

fbGearin(Slave:= stAxisStruct.Axis, Master:= GVL.axes[stAxisStruct.masterAxis.index].Axis);
fbGearIn.Execute:=FALSE;
fbGearOut(Slave:=stAxisStruct.Axis);
fbGearOut.Execute:=FALSE;

stAxisStruct.status.bFwEnabled:= stAxisStruct.inputs.bLimitFwd;
stAxisStruct.status.bBwEnabled:= stAxisStruct.inputs.bLimitBwd;


//fbMoveVelocity.Direction:=SEL(stAxisStruct.config.fVelocity<0, MC_Positive_Direction, MC_Negative_Direction);
IF (stAxisStruct.slaveAxis.index>0) THEN
	stAxisStruct.status.bFwEnabled:=stAxisStruct.status.bFwEnabled AND SEL(stAxisStruct.slaveAxis.ratio>0, GVL.axes[stAxisStruct.slaveAxis.index].inputs.bLimitBwd, GVL.axes[stAxisStruct.slaveAxis.index].inputs.bLimitFwd);
	stAxisStruct.status.bBwEnabled:=stAxisStruct.status.bBwEnabled AND SEL(stAxisStruct.slaveAxis.ratio>0, GVL.axes[stAxisStruct.slaveAxis.index].inputs.bLimitFwd, GVL.axes[stAxisStruct.slaveAxis.index].inputs.bLimitBwd);
END_IF


(*Power*)
	fbPower.Enable:=stAxisStruct.control.bEnable;
	fbPower.Enable_Positive:= stAxisStruct.status.bFwEnabled;	
	fbPower.Enable_Negative:= stAxisStruct.status.bBwEnabled;
	fbPower.Override:= stAxisStruct.config.fOverride;
	stAxisStruct.status.bEnabled:=fbPower.Status;

(*Stop*)	
	fbStop.Execute:= stAxisStruct.control.bStop;
	fbStop.Deceleration:= stAxisStruct.config.fDeceleration;
				
(*Reset*)
	fbReset.Execute:=stAxisStruct.control.bReset AND stAxisStruct.Axis.Status.Error;


CASE stAxisStruct.control.eCommand OF
	MotionFunctions.MoveAbsolute:
		fbMoveAbsolute.Execute:=stAxisStruct.control.bExecute;
		fbMoveAbsolute.Position:=stAxisStruct.config.fPosition;
		fbMoveAbsolute.Velocity:=ABS(stAxisStruct.config.fVelocity);
		fbMoveAbsolute.Acceleration:=stAxisStruct.config.fAcceleration;
		fbMoveAbsolute.Deceleration:=stAxisStruct.config.fDeceleration;
				
	MotionFunctions.MoveRelative:
		fbMoveRelative.Execute:=stAxisStruct.control.bExecute;
		fbMoveRelative.Distance:=stAxisStruct.config.fPosition;
		fbMoveRelative.Velocity:=ABS(stAxisStruct.config.fVelocity);
		fbMoveRelative.Acceleration:=stAxisStruct.config.fAcceleration;
		fbMoveRelative.Deceleration:=stAxisStruct.config.fDeceleration;
				
	MotionFunctions.Jog:
		fbJog.JogForward:=stAxisStruct.control.bJogFwd AND NOT stAxisStruct.control.bExecute;
		fbJog.JogBackwards:=stAxisStruct.control.bJogBwd AND NOT stAxisStruct.control.bExecute;
		fbJog.Mode:=0;
		fbJog.Velocity:=stAxisStruct.config.fVelocity;
		fbJog.Acceleration:=stAxisStruct.config.fAcceleration;
		fbJog.Deceleration:=stAxisStruct.config.fDeceleration;
				
	MotionFunctions.MoveVelocity:
		fbMoveVelocity.Execute:=stAxisStruct.control.bExecute;
		fbMoveVelocity.Velocity:=ABS(stAxisStruct.config.fVelocity);
		fbMoveVelocity.Acceleration:=stAxisStruct.config.fAcceleration;
		fbMoveVelocity.Deceleration:=stAxisStruct.config.fDeceleration;
		fbMoveVelocity.Direction:=SEL(stAxisStruct.config.fVelocity<0, MC_Positive_Direction, MC_Negative_Direction);

	MotionFunctions.Movemodulo:
		fbMoveModulo.Execute:=stAxisStruct.control.bExecute;
		fbMoveModulo.Position:=stAxisStruct.config.fPosition;
		fbMoveModulo.Velocity:=ABS(stAxisStruct.config.fVelocity);
		fbMoveModulo.Acceleration:=stAxisStruct.config.fAcceleration; 
		fbMoveModulo.Deceleration:=stAxisStruct.config.fDeceleration;

				
	MotionFunctions.GearIn:
		
		fbGearIn.Execute:=			stAxisStruct.control.bExecute;
		fbGearIn.RatioNumerator:=	stAxisStruct.masterAxis.ratio;
		fbGearIn.RatioDenominator:=1;		//kept static as numerator is LREAL and consistent with multigearing
		
		IF fbGearIn.InGear AND (GVL.axes[stAxisStruct.masterAxis.index].slaveAxis.index=0) THEN	//only update index and ratios on new gearIns, change of ratio requires gearOut
			GVL.axes[stAxisStruct.masterAxis.index].slaveAxis.index:=GVL.iAxis;	//Link current axis as a slave of master
			GVL.axes[stAxisStruct.masterAxis.index].slaveAxis.ratio:=stAxisStruct.masterAxis.ratio;
		END_IF
	
	MotionFunctions.GearOut:
		fbGearOut.Execute:=			stAxisStruct.control.bExecute;
		IF fbGearOut.Done THEN
			GVL.axes[stAxisStruct.masterAxis.index].slaveAxis.index:=0;
			GVL.axes[stAxisStruct.masterAxis.index].slaveAxis.ratio:=0;
		END_IF
	
	MotionFunctions.MultiGearIn:
			
	MotionFunctions.Home:
		fbHome.bExecute:= stAxisStruct.control.bExecute;
		fbHome.bReset:= stAxisStruct.control.bReset;
		fbHome.nHomeProc:= stAxisStruct.config.nHomeSeq;
		fbHome.bLimitBwd:= stAxisStruct.inputs.bLimitBwd; 
		fbHome.bLimitFwd:= stAxisStruct.inputs.bLimitFwd; 
		fbHome.bEncLatch:= stAxisStruct.inputs.bEncLatch;
		fbHome.bHomeSensor:= stAxisStruct.inputs.bHomeSensor;
		fbHome.fHomePosition:= 0; 
		stAxisStruct.status.bHomed:=fbHome.bHomed;
				
END_CASE			


(*Busy*)
stAxisStruct.status.bBusy:=stAxisStruct.Axis.Status.HasJob;

(*Error from functions and Nc*)
IF fbPower.Error AND fbPower.Active THEN
	stAxisStruct.status.bError:=fbPower.Enable;
	stAxisStruct.status.nErrorId:=fbPower.ErrorID;
ELSIF fbHalt.Error AND fbHalt.Active THEN
	stAxisStruct.status.bError:=fbHalt.Execute;
	stAxisStruct.status.nErrorId:=fbHalt.ErrorID;
ELSIF fbJog.Error THEN
	stAxisStruct.status.bError:=fbJog.JogForward OR fbJog.JogBackwards;
	stAxisStruct.status.nErrorId:=fbJog.ErrorID;
ELSIF fbMoveVelocity.Error THEN
	stAxisStruct.status.bError:=fbMoveVelocity.Execute;
	stAxisStruct.status.nErrorId:=fbMoveVelocity.ErrorID;
ELSIF fbMoveRelative.Error THEN
	stAxisStruct.status.bError:=fbMoveRelative.Execute;
	stAxisStruct.status.nErrorId:=fbMoveRelative.ErrorID;
ELSIF fbMoveAbsolute.Error THEN
	stAxisStruct.status.bError:=fbMoveAbsolute.Execute;
	stAxisStruct.status.nErrorId:=fbMoveAbsolute.ErrorID;
ELSIF fbMoveModulo.Error THEN
	stAxisStruct.status.bError:=fbMoveModulo.Execute;
	stAxisStruct.status.nErrorId:=fbMoveModulo.ErrorID;
ELSIF fbHome.bError THEN
	stAxisStruct.status.bError:=fbHome.bError;
	stAxisStruct.status.nErrorId:=fbHome.nErrorID;
ELSIF fbGearIn.Error THEN
	stAxisStruct.status.bError:=fbGearIn.Execute;
	stAxisStruct.status.nErrorId:=fbGearIn.ErrorID;
ELSIF fbGearOut.Error and stAxisStruct.Axis.Status.Coupled THEN
	stAxisStruct.status.bError:=fbGearOut.Execute;
	stAxisStruct.status.nErrorId:=fbGearOut.ErrorID;
ELSIF stAxisStruct.Axis.Status.Error  THEN
	stAxisStruct.status.bError:=TRUE;
	stAxisStruct.status.nErrorId:=stAxisStruct.Axis.Status.ErrorID;
ELSE
	stAxisStruct.status.bError:=FALSE;
	stAxisStruct.status.nErrorId:=0;
END_IF;

stAxisStruct.status.bGeared:= stAxisStruct.Axis.Status.Coupled;

(*Actual Velocity*)
stAxisStruct.status.fActVelocity:=stAxisStruct.Axis.NcToPlc.ActVelo;

(*Actual Position*)
IF stAxisStruct.Axis.Status.OpMode.Modulo THEN
	stAxisStruct.status.fActPosition:=stAxisStruct.Axis.NcToPlc.ModuloActPos;
ELSE
	stAxisStruct.status.fActPosition:=stAxisStruct.Axis.NcToPlc.ActPos;
END_IF

]]></ST>
    </Implementation>
    <LineIds Name="FB_Axis">
      <LineId Id="2448" Count="0" />
      <LineId Id="2500" Count="0" />
      <LineId Id="2440" Count="0" />
      <LineId Id="2185" Count="0" />
      <LineId Id="2203" Count="0" />
      <LineId Id="2189" Count="0" />
      <LineId Id="2377" Count="2" />
      <LineId Id="2190" Count="0" />
      <LineId Id="2380" Count="0" />
      <LineId Id="2193" Count="0" />
      <LineId Id="2381" Count="0" />
      <LineId Id="2194" Count="1" />
      <LineId Id="2383" Count="0" />
      <LineId Id="2197" Count="0" />
      <LineId Id="2384" Count="0" />
      <LineId Id="2198" Count="0" />
      <LineId Id="2385" Count="0" />
      <LineId Id="2199" Count="0" />
      <LineId Id="2437" Count="0" />
      <LineId Id="2200" Count="0" />
      <LineId Id="2438" Count="0" />
      <LineId Id="2491" Count="0" />
      <LineId Id="2490" Count="0" />
      <LineId Id="2493" Count="0" />
      <LineId Id="2201" Count="0" />
      <LineId Id="2446" Count="0" />
      <LineId Id="1627" Count="1" />
      <LineId Id="2515" Count="0" />
      <LineId Id="2522" Count="1" />
      <LineId Id="2519" Count="0" />
      <LineId Id="2518" Count="0" />
      <LineId Id="2520" Count="0" />
      <LineId Id="2525" Count="0" />
      <LineId Id="2521" Count="0" />
      <LineId Id="2517" Count="0" />
      <LineId Id="2516" Count="0" />
      <LineId Id="1629" Count="0" />
      <LineId Id="1632" Count="3" />
      <LineId Id="1637" Count="0" />
      <LineId Id="1642" Count="0" />
      <LineId Id="1921" Count="0" />
      <LineId Id="1925" Count="1" />
      <LineId Id="2205" Count="0" />
      <LineId Id="2207" Count="0" />
      <LineId Id="2209" Count="0" />
      <LineId Id="2375" Count="1" />
      <LineId Id="1657" Count="0" />
      <LineId Id="2366" Count="0" />
      <LineId Id="2220" Count="0" />
      <LineId Id="2222" Count="3" />
      <LineId Id="2238" Count="0" />
      <LineId Id="2236" Count="0" />
      <LineId Id="2239" Count="0" />
      <LineId Id="2241" Count="1" />
      <LineId Id="2442" Count="0" />
      <LineId Id="2244" Count="0" />
      <LineId Id="2254" Count="0" />
      <LineId Id="2219" Count="0" />
      <LineId Id="2258" Count="0" />
      <LineId Id="2443" Count="0" />
      <LineId Id="2260" Count="0" />
      <LineId Id="2262" Count="2" />
      <LineId Id="2274" Count="0" />
      <LineId Id="2273" Count="0" />
      <LineId Id="2276" Count="3" />
      <LineId Id="2281" Count="0" />
      <LineId Id="2290" Count="1" />
      <LineId Id="2294" Count="4" />
      <LineId Id="2303" Count="0" />
      <LineId Id="2310" Count="0" />
      <LineId Id="2343" Count="0" />
      <LineId Id="2504" Count="0" />
      <LineId Id="2495" Count="2" />
      <LineId Id="2506" Count="2" />
      <LineId Id="2510" Count="0" />
      <LineId Id="2509" Count="0" />
      <LineId Id="2361" Count="1" />
      <LineId Id="2498" Count="0" />
      <LineId Id="2511" Count="1" />
      <LineId Id="2514" Count="0" />
      <LineId Id="2513" Count="0" />
      <LineId Id="2444" Count="0" />
      <LineId Id="2364" Count="0" />
      <LineId Id="2344" Count="0" />
      <LineId Id="2311" Count="0" />
      <LineId Id="2329" Count="7" />
      <LineId Id="2339" Count="0" />
      <LineId Id="2309" Count="0" />
      <LineId Id="2216" Count="0" />
      <LineId Id="1674" Count="0" />
      <LineId Id="1733" Count="0" />
      <LineId Id="1936" Count="1" />
      <LineId Id="1941" Count="34" />
      <LineId Id="1979" Count="3" />
      <LineId Id="2501" Count="0" />
      <LineId Id="1992" Count="0" />
      <LineId Id="2502" Count="0" />
      <LineId Id="1993" Count="8" />
      <LineId Id="2272" Count="0" />
      <LineId Id="1934" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>